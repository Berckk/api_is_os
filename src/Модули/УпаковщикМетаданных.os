#Использовать tempfiles

Перем Лог;

// Распаковать контейнер метаданных
//
// Параметры:
//   ФайлРаспаковки - <Строка> - путь файла для распаковки
//   КаталогРаспаковки - <Строка> - каталог для распаковки
//
Процедура РаспаковатьКонтейнерМетаданных(Знач ФайлРаспаковки, Знач КаталогРаспаковки) Экспорт
	ПолучитьЛог();

	ФайлДляРаспаковки = Новый файл(ФайлРаспаковки);
	Если КаталогРаспаковки = "" тогда
		КаталогРаспаковки = ВременныеФайлы.СоздатьКаталог();
	КонецЕсли;
	ФайлПрограммыРаспаковки = "v8unpack.exe";
	СтрокаЗапуска = "" + ФайлПрограммыРаспаковки+" -parse """+ФайлДляРаспаковки.ПолноеИмя+""" """+КаталогРаспаковки+"""";
	// СтрокаЗапуска = """"+ФайлПрограммыРаспаковки+""" -parse """+ФайлДляРаспаковки.ПолноеИмя+""" """+КаталогРаспаковки+"""";
	Если НЕ ПараметрыСистемы.ЭтоWindows Тогда 
		СтрокаЗапуска = "sh -c '"+СтрокаЗапуска+"'";
	КонецЕсли;
	ВыполнитьКоманду(СтрокаЗапуска);

	ФайлМодуля = Новый Файл(ОбъединитьПути(КаталогРаспаковки, "module"));
	Если ФайлМодуля.Существует() Тогда 
		ФайлКуда = Новый Файл(ОбъединитьПути(ФайлМодуля.Путь, "Module.bsl"));
		Лог.Отладка("Перемещаю файл <%1> в <%2>", ФайлМодуля.ПолноеИмя, ФайлКуда.ПолноеИмя);
		Если ФайлКуда.Существует() Тогда
			УдалитьФайлы(ФайлКуда.ПолноеИмя);
		КонецЕсли;
		ПереместитьФайл(ФайлМодуля.ПолноеИмя, ФайлКуда.ПолноеИмя);
	КонецЕсли; 

КонецПроцедуры

// Упаковать контейнер метаданных
//
// Параметры:
//   КаталогРаспаковки - <Строка> - каталог для распаковки
//   ФайлРаспаковки - <Строка> - путь файла для упаковки
//
Процедура УпаковатьКонтейнерМетаданных(Знач КаталогРаспаковки, Знач ФайлРаспаковки) Экспорт
	ПолучитьЛог();

	ФайлДляРаспаковки = Новый файл(ФайлРаспаковки);
	Если КаталогРаспаковки = "" тогда
		КаталогРаспаковки = ВременныеФайлы.СоздатьКаталог();
	КонецЕсли;
	ФайлПрограммыРаспаковки = "v8unpack.exe";
	ФайлМодуля = Новый Файл(ОбъединитьПути(КаталогРаспаковки, "Module.bsl"));
	Если ФайлМодуля.Существует() Тогда
		КопироватьФайл(ФайлМодуля.ПолноеИмя, ОбъединитьПути(ФайлМодуля.Путь, "module") );
	КонецЕсли;
	СтрокаЗапуска = """"+ФайлПрограммыРаспаковки+""" -build -nopack """+КаталогРаспаковки+""" """+ФайлДляРаспаковки.ПолноеИмя+"""";
	Если НЕ ПараметрыСистемы.ЭтоWindows Тогда 
		СтрокаЗапуска = "sh -c '"+СтрокаЗапуска+"'";
	КонецЕсли;

	ВыполнитьКоманду(СтрокаЗапуска);

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач СтрокаЗапуска)
	Лог.Отладка(СтрокаЗапуска);

	Команда = Новый Команда;
	Команда.УстановитьСтрокуЗапуска(СтрокаЗапуска);
	Команда.Исполнить();
КонецПроцедуры

Функция ПолучитьЛог()
	Если Лог = Неопределено Тогда
		Лог = Логирование.ПолучитьЛог(ПараметрыСистемы.ИмяЛогаСистемы());
	КонецЕсли;
	Возврат Лог;	
КонецФункции
